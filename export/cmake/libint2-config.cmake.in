# - CMAKE Config file for the Libint2 package
# This will define the following CMake cache variables
#
#    LIBINT2_FOUND           - true if libint2.h header and libint2 library were found
#    LIBINT2_VERSION         - the libint2 version
#    LIBINT2_EXT_VERSION     - the libint2 version including the (optional) buildid, such as beta.3
#    LIBINT2_INCLUDE_DIRS    - (deprecated: use the CMake IMPORTED targets listed below) list of libint2 include directories
#    LIBINT2_LIBRARIES       - (deprecated: use the CMake IMPORTED targets listed below) list of libint2 libraries
#
# and the following imported targets
#
#     Libint2::int2          - library only
#     Libint2::cxx           - (if Eigen + Boost was found at the library configure time) library + C++11 API
#
# Author: Eduard Valeyev - libint@valeyev.net

# Set package version
set(LIBINT2_VERSION "@LIBINT_VERSION@")
set(LIBINT2_EXT_VERSION "@LIBINT_EXT_VERSION@")

@PACKAGE_INIT@

set(LIBINT2_LIBRARIES Libint2::int2)
set(LIBINT2_INCLUDE_DIRS "@PACKAGE_LIBINT2_INSTALL_INCLUDEDIR@")

# Include library IMPORT targets
if(NOT TARGET Libint2::libint2)
  include("${CMAKE_CURRENT_LIST_DIR}/libint2-targets.cmake")
  if(NOT TARGET Libint2::libint2)
    message(FATAL_ERROR "expected Libint2::libint2 among imported Libint2 targets")
  endif()
endif()

# alias new target names to old namespaced targets
add_library(Libint2::int2 ALIAS Libint2::libint2)
if (TARGET Libint2::libint2-static)
  add_library(Libint2::int2-static ALIAS Libint2::libint2-static)
  add_library(Libint2::int2_static ALIAS Libint2::libint2-static)
endif(TARGET Libint2::libint2-static)
if (TARGET Libint2::libint2_Eigen)
  add_library(Libint2::Eigen ALIAS Libint2::libint2_Eigen)
endif (TARGET Libint2::libint2_Eigen)
if (TARGET Libint2::libint2_cxx)
  add_library(Libint2::cxx ALIAS Libint2::libint2_cxx)
endif (TARGET Libint2::libint2_cxx)

set(LIBINT2_FOUND TRUE)
